import java.time.Duration

buildscript {
    ext {
        kotlin_version = '1.4.10'
        infra_commons_version='2.1.0'
        infra_telemetry_version='1.1.0'
        slf4j_version='1.7.30'

        spring_boot_version = '2.4.5'
        spring_cloud_version='2020.0.2'
        spring_version = '5.3.6'

        kafka_version='2.8.0'
        kryo_version = '5.0.0'
        mysql_version='8.0.22'

        mybatis_generator_version='1.4.0'
        mybatis_gen_plug_version='1.4'

        open_telemetry_version='1.1.0'

        junit_verions = '5.7.0'

        mockito_version = '1.10.19'

        maven_repo= System.getenv("MAVEN_PROXY") //?: 'http://maven.aliyun.com/nexus/content/groups/public/'
        publishing_plugin_version = '1.1.0'

        repositories {
            mavenLocal()
            if(maven_repo != null) {
                maven {
                    allowInsecureProtocol true
                    url maven_repo
                }
            }
            mavenCentral()
            maven { url 'https://repo.spring.io/plugins-release' }
            maven { url '"https://plugins.gradle.org/m2/"' }
        }
    }
}

plugins {
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlin_version" apply false
    id "org.springframework.boot" version "$spring_boot_version" apply false
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version" apply false
    id "com.arenagod.gradle.MybatisGenerator" version "$mybatis_gen_plug_version" apply false
    id "io.github.gradle-nexus.publish-plugin" version "$publishing_plugin_version" apply false
}
apply plugin: 'io.github.gradle-nexus.publish-plugin'

static Boolean  isNotNullOrBlank(Object s){
    return !(s == null || s.toString().trim().length() == 0)
}

static canBeSign(Project project) {
    return isNotNullOrBlank(project.findProperty("signing.password")) &&
            isNotNullOrBlank(project.findProperty('signing.secretKeyRingFile')) &&
            isNotNullOrBlank(project.findProperty('signing.keyId'))
}

static getPropertyOrCmdArgs(Project project, String propertyName, String cmdArgName) {
    def propertyValue = project.hasProperty(propertyName) ? project.property(propertyName).toString() : null
    def v = (System.properties.getProperty(cmdArgName) ?: propertyValue ) ?: System.getenv(propertyName)
    return isNotNullOrBlank(v) ? v : null
}

allprojects {
    group 'com.labijie.infra'
    version '2.1.0'

    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'kotlin-spring'
    apply plugin: 'java-library'

    if(!project.name.startsWith("dummy")) {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'
    }

    compileJava.options.encoding = "UTF-8"
    javadoc.options.encoding = "UTF-8"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError = false
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        if(maven_repo != null) {
            maven {
                allowInsecureProtocol true
                url maven_repo
            }
        }
        mavenCentral()
    }

    configurations.all {
        exclude group: "junit", module: "junit"
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    dependencies {
        implementation platform("org.jetbrains.kotlin:kotlin-bom:${kotlin_version}")
        testImplementation platform("org.junit:junit-bom:$junit_verions")
        implementation platform("io.opentelemetry:opentelemetry-bom:${open_telemetry_version}")
        implementation platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}")

        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlin:kotlin-reflect"

        testImplementation "org.junit.jupiter:junit-jupiter"
        testImplementation("org.jetbrains.kotlin:kotlin-test-junit5"){
            exclude group:"org.junit"
        }

        testImplementation "org.mockito:mockito-all:$mockito_version"
    }



    test {
        useJUnitPlatform()
    }
}

subprojects {
    def u = getPropertyOrCmdArgs(project, "PUB_USER", "u")
    def p =getPropertyOrCmdArgs(project, "PUB_PWD", "p")
    def s = getPropertyOrCmdArgs(project, "PUB_URL", "s")

    if(!project.name.startsWith("dummy")) {

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = "mqts-" + project.name
                    from components.java
                    pom {
                        name = "mqts-" + project.name
                        description = 'A distributed transaction library based on message queues (Similar to TCC but simpler)'
                        url = 'https://github.com/hongque-pro/infra-mqts'
//                    properties = [
//                            myProp: "value",
//                            "prop.with.dots": "anotherValue"
//                    ]
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'labijie infra team'
                                name = 'infra team'
                                email = 'tech@labijie.com'
                            }
                        }
                        scm {
                            url = 'https://github.com/hongque-pro/infra-oauth2'
                            connection = 'scm:git@github.com:hongque-pro/infra-mqts.git'
                            developerConnection = 'git@github.com:hongque-pro/infra-mqts.git'
                        }

                    }
                }
            }

            signing {
                if (canBeSign(project)) {
                    sign publishing.publications.mavenJava
                } else {
                    println "Signing information missing/incomplete for ${project.name}"
                }
            }

            repositories {
                if (isNotNullOrBlank(u) && isNotNullOrBlank(p) && isNotNullOrBlank(s)) {
                    maven {
                        name = "nexus"
                        url = s
                        credentials {
                            username = u
                            password = p
                        }
                    }
                }
            }
        }
    }
}


nexusPublishing {
    repositories {
        sonatype {
            def u = getPropertyOrCmdArgs(project, "PUB_USER", "u")
            def p =getPropertyOrCmdArgs(project, "PUB_PWD", "p")
            username = u
            password = p
        }
    }
}
